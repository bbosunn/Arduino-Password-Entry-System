Arduino-Password-Entry-System

#include <LiquidCrystal.h>
#include <Keypad.h>

// Initialize LCD (RS, E, D4, D5, D6, D7)
LiquidCrystal lcd(12, 11, 5, 4, 3, 2);

// Set the correct password
const String correctPassword = "2004";
String enteredPassword = "";

// Define keypad configuration
const byte ROWS = 4;
const byte COLS = 4;
char keys[ROWS][COLS] = {
  {'1', '2', '3', 'A'},
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};

// Define Keypad Pins
byte rowPins[ROWS] = {6, 7, 8, 9}; 
byte colPins[COLS] = {10, 13, A0, A1};

// Create keypad object
Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

// LED Pins
const int successLED = A2;  // Green LED for correct password
const int errorLED = A3;    // Red LED for incorrect password

void setup() {
  lcd.begin(16, 2);
  lcd.print("Enter Code:");

  pinMode(successLED, OUTPUT);
  pinMode(errorLED, OUTPUT);
  
  digitalWrite(successLED, LOW);  // Ensure LEDs are OFF at startup
  digitalWrite(errorLED, LOW);
}

void loop() {
  char key = keypad.getKey();

  if (key) {
    if (key == '#') {  // Submit password
      lcd.clear();
      if (enteredPassword == correctPassword) {
        lcd.print("Access Granted");
        blinkLED(successLED, 5);  // Blink 5 times for correct password
      } else {
        lcd.print("Access Denied!");
        blinkLED(errorLED, 5);  // Blink 5 times for incorrect password
      }
      delay(2000);
      enteredPassword = "";
      lcd.clear();
      lcd.print("Enter Password:");
    }
    else if (key == '*') {  // Clear input
      enteredPassword = "";
      lcd.clear();
      lcd.print("Enter Password:");
    }
    else {  // Add key to entered password
      if (enteredPassword.length() < 10) {
        enteredPassword += key;
        lcd.setCursor(0, 1);
        lcd.print(enteredPassword);
      }
    }
  }
}

// Function to blink LED
void blinkLED(int ledPin, int times) {
  for (int i = 0; i < times; i++) {
    digitalWrite(ledPin, HIGH);  // Turn LED ON
    delay(300);
    digitalWrite(ledPin, LOW); // Turn LED OFF
    delay(300);
  }
}